plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.6.0'
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceCompatibility = '11'
mainClassName = 'com.salestaxes.SalesTaxesApp'

check.dependsOn jacocoTestCoverageVerification
test.finalizedBy jacocoTestReport

checkstyle {
    toolVersion '7.8.1'
    configFile file("config/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.beust:jcommander:1.78'

    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.5'
    testImplementation 'com.jparams:to-string-verifier:1.4.8'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.mockito:mockito-core:3.7.7'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.7.7'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

test {
    useJUnitPlatform()
}
